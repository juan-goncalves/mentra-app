plugins {
    id "com.android.application"
    id "com.google.android.gms.oss-licenses-plugin"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "me.juangoncalves.mentra"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 3
        versionName "1.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "Mentra Debug"
            manifestPlaceholders.crashlyticsCollectionEnabled = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            resValue "string", "app_name", "Mentra"
            manifestPlaceholders.crashlyticsCollectionEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }

    kapt {
        correctErrorTypes true
    }
}

dependencies {
    implementation project(path: ':domain-layer')
    implementation project(path: ':data-layer')
    implementation project(path: ':android-cache')
    implementation project(path: ':android-network')
    implementation project(path: ':pie-chart-view')

    implementation Deps.kotlin_stdlib
    implementation Deps.android_coroutines

    implementation Deps.androidx_core_ktx
    implementation Deps.androidx_appcompat
    implementation Deps.androidx_activity_ktx
    implementation Deps.androidx_fragment_ktx
    implementation Deps.androidx_preference_ktx
    implementation Deps.androidx_constraint_layout
    implementation Deps.androidx_swipe_refresh_layout
    implementation Deps.androidx_viewpager2
    implementation Deps.androidx_work_ktx
    implementation Deps.androidx_lifecycle_viewmodel_ktx
    implementation Deps.androidx_lifecycle_saved_state
    implementation Deps.androidx_lifecycle_livedata_ktx
    implementation Deps.google_material
    implementation Deps.google_oss_licenses

    implementation Deps.dagger_hilt
    implementation Deps.androidx_hilt_work
    implementation Deps.androidx_hilt_lifecycle_viewmodel

    implementation Deps.glide
    implementation Deps.fading_edge_layout
    implementation Deps.mp_android_chart

    implementation platform(Deps.firebase_platform)
    implementation Deps.firebase_analytics
    implementation Deps.firebase_crashlytics

    kapt Deps.dagger_hilt_compiler
    kapt Deps.androidx_hilt_compiler
    kapt Deps.androidx_lifecycle_compiler
    kapt Deps.glide_compiler

    kaptTest Deps.dagger_hilt_compiler

    testImplementation project(path: ':test-utils')
    testImplementation Deps.junit
    testImplementation Deps.hamcrest
    testImplementation Deps.mockk
    testImplementation Deps.androidx_core_testing
    testImplementation Deps.dagger_hilt_android_testing
    testImplementation Deps.robolectric
    testImplementation Deps.coroutines_test

    androidTestImplementation Deps.androidx_junit
    androidTestImplementation Deps.espresso
    androidTestImplementation Deps.androidx_work_testing
}